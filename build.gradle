buildscript {
    ext.kotlin_version = '1.2.31'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

group 'org.swiften'
version '1.0-SNAPSHOT'

apply plugin: "kotlin"

jacoco {
    toolVersion = "0.8.1"
    reportsDir = file("$buildDir/jacoco/")
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    classDirectories = files("${buildDir}/classes")

    sourceDirectories = files(
            sourceSets.main.java.srcDirs,
            sourceSets.main.kotlin.srcDirs
    )

    executionData = files("$buildDir/jacoco/jacocoTest.exec")
}

coveralls {
    jacocoReportPath "$buildDir/jacoco/test/jacocoTestReport.xml"
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-testng:$kotlin_version"
    testCompile "org.testng:testng:+"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.6"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.6"
}

test {
    useTestNG()

    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }
}